plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
    id 'application'
}

group = 'fr.raksrinana'
version = project.bot_version
description = 'RSNDiscord'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_12

ext.moduleName = 'fr.raksrinana.rsndiscord'
mainClassName = project.mainClass

dependencies {
    implementation("net.dv8tion:JDA:${project.JDA_version}") {
        exclude module: 'opus-java'
    }
    implementation("com.sedmelluq:lavaplayer:${project.lavaplayer_version}")
    implementation("com.github.natanbc:lavaplayer-arm-natives:${project.lavaplayer_arm_version}")

    implementation("org.slf4j:slf4j-api:${project.slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${project.logback_version}")
    implementation("fr.mrcraftcod:utils.http:${project.raksrinana_utils_version}")

    implementation("com.konghq:unirest-java:${project.unirest_version}")
    implementation("com.beust:jcommander:${project.jcommander_version}")
    implementation("com.fasterxml.jackson.core:jackson-core:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${project.jackson_version}")
    implementation("org.apache.httpcomponents:httpclient:${project.httpclient_version}")
    implementation("org.reflections:reflections:${project.reflections_version}")
}

repositories {
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://jcenter.bintray.com'
    }
    maven {
        url = 'https://maven.mrcraftcod.fr'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from 'src/main/filteredResources'
    filter(ReplaceTokens, tokens: [bot_version: project.version, bot_name: project.name])
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-Class': project.mainClass
    }
}

shadowJar {
    baseName = project.name
    classifier = 'shaded'
    version = null
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
