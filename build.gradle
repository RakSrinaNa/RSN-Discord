plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
    id 'application'
}

group = 'fr.raksrinana'
version = project.bot_version
description = 'RSNDiscord'
sourceCompatibility = targetCompatibility = '12'

ext.moduleName = 'fr.raksrinana.rsndiscord'
mainClassName = project.mainClass

dependencies {
    implementation("net.dv8tion:JDA:${project.JDA_version}") {
        exclude module: 'opus-java'
    }
    implementation("com.sedmelluq:lavaplayer:${project.lavaplayer_version}")
    implementation("com.github.natanbc:lavaplayer-arm-natives:${project.lavaplayer_arm_version}")

    implementation("org.slf4j:slf4j-api:${project.slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${project.logback_version}")
    implementation("fr.mrcraftcod:utils.http:${project.raksrinana_utils_version}")

    implementation("org.json:json:${project.json_version}")
    implementation("com.beust:jcommander:${project.jcommander_version}")
    implementation("com.fasterxml.jackson.core:jackson-core:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${project.jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${project.jackson_version}")
    implementation("org.apache.httpcomponents:httpclient:${project.httpclient_version}")
    implementation("org.reflections:reflections:${project.reflections_version}")
}

processResources {
    from ('src/main/java/resources') {
        include '*.properties'
    }
}


repositories {
    mavenLocal()
    maven {
        url = 'https://maven.pkg.github.com/RakSrinaNa'
    }
    maven {
        url = 'https://maven.mrcraftcod.fr/'
    }
    maven {
        url = 'https://jcenter.bintray.com'
    }
    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-Class': project.mainClass
    }
}

shadowJar {
    baseName = project.name
    classifier = null
    version = null
    minimize()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
